/*******************************************************************************
 * Copyright (c) 2010, 2013 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *     E.D.Willink (CEA LIST) - Bug 399582
 *
 * Run As->MWE2 Workflow to regenerate the editor.
 * If it fails with a stack dump, open the Xtext file and do
 * a dummy edit. Generally there is an error in the source file.
 *******************************************************************************/
module GenerateEssentialOCLEditor

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.ui.codemining.CodeMiningFragment

var essentialOCLprojectName = "org.eclipse.ocl.xtext.essentialocl"
var essentialOCLruntimeProject = "../../plugins/${essentialOCLprojectName}"

var rootPath = "../../plugins"

/**
 *	Generate the EssentialOCL Editor.
 */
Workflow {
      bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		platformUri = ".."
		registerGeneratedEPackage = "${essentialOCLprojectName}cs.EssentialOCLCSPackage"
		registerGenModelFile = "platform:/resource/${essentialOCLprojectName}/model/EssentialOCLCS.genmodel"
	}
	
	component = XtextGenerator {
		standaloneSetup = XtextGeneratorStandaloneSetup {
			scanClasspath = false
		}
		configuration = org.eclipse.ocl.examples.build.fragments.MyDefaultGeneratorModule {
			project = org.eclipse.ocl.examples.build.fragments.MyStandardProjectConfig {
				activatorName = "EssentialOCLActivator"
				baseName = "${essentialOCLprojectName}"
				rootPath = rootPath
				runtimeTest = {
					enabled = false
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				genericIde = {
					enabled = true
					name = "${essentialOCLprojectName}.ui"
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				encoding = "UTF-8"
				fileHeader =
"/*******************************************************************************
 * Copyright (c) 2011, \${year} Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *******************************************************************************/"
			}
		}
		language = StandardLanguage {
			name = "${essentialOCLprojectName}.EssentialOCL"
			fileExtensions = "essentialocl"
			generator = null
			serializer = org.eclipse.ocl.examples.build.fragments.DeclarativeSerializerFragmentXtend {
				generateStub = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
		/*	fragment = CodeMiningFragment {
				generateStub = true
				generateXtendStub = false
			} */
			parserGenerator = parser.antlr.XtextAntlrGeneratorFragment2 {
				options = parser.antlr.AntlrOptions {
					backtrack = true
					classSplitting = true
				}
			}
			formatter = org.eclipse.ocl.examples.build.fragments.DeclarativeFormatterFragmentXtend {
				generateStub = true
			}
			// EssentialOCL bindings 
			fragment = org.eclipse.ocl.examples.build.fragments.EssentialOCLFragment {}
			// Support Markup and Hover text 
			fragment = org.eclipse.ocl.examples.build.fragments.MarkupHoverFragment {}
			// *.xtextbin support on Xtext 2.3		
			fragment = org.eclipse.ocl.examples.build.fragments.CompatibilityFragment {}
		}
	}
	component = org.eclipse.ocl.examples.build.xtend.GenerateGrammarXtend {
		javaFolder = "${essentialOCLruntimeProject}/src-gen"
		javaPackageName = "${essentialOCLprojectName}"
		grammarFileStem = "EssentialOCL"
	}
	component = org.eclipse.ocl.examples.build.utilities.InjectCompatibilityAbstractInternalAntlrParser {
		fileName = "${essentialOCLruntimeProject}/src-gen/org/eclipse/ocl/xtext/essentialocl/parser/antlr/internal/InternalEssentialOCLParser.java"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}/META-INF"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}/emf-gen"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}/src"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}/src-gen"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}.ui/META-INF"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}.ui/src"
	}
	component = org.eclipse.ocl.examples.build.utilities.ConvertToUnixLineEndings {
		directory = "${essentialOCLruntimeProject}.ui/src-gen"
	}
}
