/*******************************************************************************
 * Copyright (c) 2011 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * Run As->MWE2 Workflow to regenerate the editor.
 * If it fails with a stack dump, open the Xtext file and do
 * a dummy edit. Generally there is an error in the source file.
 *******************************************************************************/
module GenerateMarkupEditor

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.ui.codemining.CodeMiningFragment

var markupProjectName = "org.eclipse.ocl.xtext.markup"
var markupRuntimeProject = "../../plugins/${markupProjectName}"

var rootPath = "../../plugins"

/**
 *	Generate the markup Editor.
 */
Workflow {
    bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		platformUri = ".."
		registerGeneratedEPackage = "org.eclipse.ocl.xtext.markupcs.MarkupPackage"
		registerGenModelFile = "platform:/resource/org.eclipse.ocl.xtext.markup/model/Markup.genmodel"
	}
	
	component = XtextGenerator {
		standaloneSetup = XtextGeneratorStandaloneSetup {
			scanClasspath = false
		}
		configuration = org.eclipse.ocl.build.fragments.MyDefaultGeneratorModule {
			project = org.eclipse.ocl.build.fragments.MyStandardProjectConfig {
				activatorName = "MarkupActivator"
				baseName = "${markupProjectName}"
				rootPath = rootPath
				runtimeTest = {
					enabled = false
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				genericIde = {
					enabled = true
					name = "${markupProjectName}.ui"
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				lineDelimiter = "\n"
				encoding = "UTF-8"
				fileHeader =
"/*******************************************************************************
 * Copyright (c) 2011, \${year} Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *******************************************************************************/"
			}
		}
		language = org.eclipse.ocl.build.fragments.MyStandardLanguage {
			name = "${markupProjectName}.Markup"
			fileExtensions = "markupocl"
			grammarUri = "platform:/resource/${markupProjectName}/src/org/eclipse/ocl/xtext/markup/Markup.xtext"
			generator = null
			serializer = org.eclipse.ocl.build.fragments.DeclarativeSerializerFragmentXtend {
				generateStub = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
		/*	fragment = CodeMiningFragment {
				generateStub = true
				generateXtendStub = false
			} */
			parserGenerator = parser.antlr.XtextAntlrGeneratorFragment2 {
				options = parser.antlr.AntlrOptions {
					backtrack = true
					classSplitting = true
				}
			}
			formatter = org.eclipse.ocl.build.fragments.DeclarativeFormatterFragmentXtend {
				generateStub = true
			}
			// *.xtextbin support on Xtext 2.3		
			fragment = org.eclipse.ocl.build.fragments.CompatibilityFragment {}
		}
	}
	component = org.eclipse.ocl.build.xtend.GenerateGrammarXtend {
		javaFolder = "${markupRuntimeProject}/src-gen"
		javaPackageName = "${markupProjectName}"
		grammarFileStem = "Markup"
	}
	component = org.eclipse.ocl.build.utilities.InjectCompatibilityAbstractInternalAntlrParser {
		fileName = "${markupRuntimeProject}/src-gen/org/eclipse/ocl/xtext/markup/parser/antlr/internal/InternalMarkupParser.java"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}/META-INF"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}/emf-gen"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}/src"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}/src-gen"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}.ui/META-INF"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}.ui/src"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${markupRuntimeProject}.ui/src-gen"
	}
}
