/*******************************************************************************
 * Copyright (c) 2014 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * Run As->MWE2 Workflow to regenerate the editor.
 * If it fails with a stack dump, open the Xtext file and do
 * a dummy edit. Generally there is an error in the source file.
 *******************************************************************************/
module GenerateIdiomsEditor

import org.eclipse.emf.ecore.resource.impl.*
import org.eclipse.ocl.build.utilities.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.ui.codemining.CodeMiningFragment

var idiomsProjectName = "org.eclipse.ocl.xtext.idioms"
var idiomsRuntimeProject = "../../plugins/${idiomsProjectName}"

var rootPath = "../../plugins"

/**
 *	Generate the Idioms Editor.
 */
Workflow {
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		platformUri = ".."
		registerGeneratedEPackage = "org.eclipse.emf.ecore.EcorePackage"
		registerGeneratedEPackage = "org.eclipse.ocl.xtext.idioms.IdiomsPackage"
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.ocl.xtext.idioms/model/Idioms.genmodel"
	}
	
	component = XtextGenerator {
		standaloneSetup = XtextGeneratorStandaloneSetup {
			scanClasspath = false
		}
		configuration = org.eclipse.ocl.build.fragments.MyDefaultGeneratorModule {
			project = org.eclipse.ocl.build.fragments.MyStandardProjectConfig {
				activatorName = "IdiomsActivator"
				baseName = "${idiomsProjectName}"
				rootPath = rootPath
				runtimeTest = {
					enabled = false
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				genericIde = {
					enabled = true
					name = "${idiomsProjectName}.ui"
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				lineDelimiter = "\n"
				encoding = "UTF-8"
				fileHeader =
"/*******************************************************************************
 * Copyright (c) 2011, \${year} Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *******************************************************************************/"
			}
		}
		language = org.eclipse.ocl.build.fragments.MyStandardLanguage {
			name = "${idiomsProjectName}.Idioms"
			fileExtensions = "idioms"
			grammarUri = "platform:/resource/${idiomsProjectName}/src/org/eclipse/ocl/xtext/idioms/Idioms.xtext"
			generator = null
			// Comment the serializer out during a transient rebuild after an incompatible Idioms.ecore/idioms/xtext change
			serializer = org.eclipse.ocl.build.fragments.DeclarativeSerializerFragmentXtend {
				generateStub = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
		/*	fragment = CodeMiningFragment {
				generateStub = true
				generateXtendStub = false
			} */
			parserGenerator = parser.antlr.XtextAntlrGeneratorFragment2 {
				options = parser.antlr.AntlrOptions {
					backtrack = true
					classSplitting = true
				}
			}
			// Comment the formatter out during a transient rebuild after an incompatible Idioms.ecore/idioms/xtext change
			formatter = org.eclipse.ocl.build.fragments.DeclarativeFormatterFragmentXtend {
				generateStub = true
			}
			// *.xtextbin support on Xtext 2.3		
			fragment = org.eclipse.ocl.build.fragments.CompatibilityFragment {}
		}
	}
	component = org.eclipse.ocl.build.xtend.GenerateGrammarXtend {
		javaFolder = "${idiomsRuntimeProject}/src-gen"
		javaPackageName = "${idiomsProjectName}"
		grammarFileStem = "Idioms"
	}
	component = org.eclipse.ocl.build.utilities.InjectCompatibilityAbstractInternalAntlrParser {
		fileName = "${idiomsRuntimeProject}/src-gen/org/eclipse/ocl/xtext/idioms/parser/antlr/internal/InternalIdiomsParser.java"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}/META-INF"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}/emf-gen"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}/src"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}/src-gen"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}.ui/META-INF"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}.ui/src"
	}
	component = org.eclipse.ocl.build.utilities.ConvertToUnixLineEndings {
		directory = "${idiomsRuntimeProject}.ui/src-gen"
	}
}
